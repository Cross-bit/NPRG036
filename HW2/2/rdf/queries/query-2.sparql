PREFIX ex: <http://example.org/vocabulary/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

# Vybere všechny trojice identifikátorů tříd, jmen jejich třídních učitelů a názvů předmětů, kde daný učitel může učit svoji třídu.

SELECT DISTINCT ?classID ?classTeacherName ?subjectName
WHERE {
	?class a ex:Class ;
		ex:ClassCode ?classCode ;
		ex:ClassYear ?classYear .
	
	BIND(CONCAT(STR(?classYear), ".", STR(?classCode)) AS ?classID)
	
	?teacher a ex:Teacher ;
		foaf:name ?classTeacherName .
	?subject a ex:Subject ;
		ex:SubjectName ?subjectName .
	
	?class ex:ClassTeacher ?teacher ;
		^ex:BelongingClass/ex:PlannedLesson/ex:IsTaught ?subject .
	
	?teacher ex:Teaches ?subject .   
}